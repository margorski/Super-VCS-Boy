var temp1=0x80, temp2, temp3, temp4, temp5, temp6, line1, line2,flip
var camera_y[2]=0x90, camera_y_offset, camera_y_cover
var ptr_map_pf0_0[2]=0xA0, ptr_map_pf1_0[2], ptr_map_pf2_0[2], ptr_map_pf0_1[2], ptr_map_pf1_1[2], ptr_map_pf2_1[2]
var player_x=0xB0, player_y, player_y_max, draw_spr

data sprite {
	0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0 0 0 0 0 0 0 0
}

// CONSTANTS
[CAMERA_Y_LIMIT=231]
[SPRITE_HEIGHT=8]

inline draw_map_x_scanlines { 
	nocross {
		{
			wsync
			a=flip a^0xff flip=a == {
				pf0=a=(ptr_map_pf0_0),y
				pf1=a=(ptr_map_pf1_0),y
				pf2=a=(ptr_map_pf2_0),y
				pf1=a=(ptr_map_pf1_1),y
				pf2=a=(ptr_map_pf2_1),y
				pf0=a=(ptr_map_pf0_1),y
			}
			else {
				pf0=pf1=pf2=a=0 
				a=[SPRITE_HEIGHT] a?--line1 <{ gp0=a=0 } else { gp0=a=0xff }


			}

			x--
		} !=
	}
}

inline draw_map_frame {
	// top border
	gp0=cpf=draw_spr=a=0x0 y=8 line1=a=player_y line2=a=player_y_max
	!= {
		{
			wsync y--
		} !=
	}

	// first row of map is trimmed by camera movement
	y=23 cpf=a=0x80 
	x=camera_y_offset != {
		draw_map_x_scanlines 
	}
	y--
	
	// drawing rest of map
	{
		x=8
		draw_map_x_scanlines
		y--
	} !=
	
	// last row of map is trimmed by camera movement
	x=camera_y_cover != {
		draw_map_x_scanlines
	}
	
	wsync 
	pf0=pf1=pf2=a=0 cpf=a=0x0 
}

inline handle_camera_movement {
	a=swcha a&0x10 == {
		a=camera_y+1 a?CAMERA_Y_LIMIT < { c- a=camera_y a+0x20 camera_y=a a=camera_y+1 a+0 camera_y+1=a }
	}

	a=swcha a&0x20 == {
		a=camera_y a&0xE0 == {
			a=camera_y+1 != {
				c+ a-1 camera_y+1=a
				camera_y=a=0xE0
			}
		}
		else { c+ a-0x20 camera_y=a }
	}
	a=camera_y a>> a>> a>> a>> a>> camera_y_offset=a
	a=8 c+ a-camera_y_offset camera_y_cover=a
}

[PLAYER_Y_TOP_CLAMP=20]
[PLAYER_Y_BOTTOM_CLAMP=80]
[PLAYER_X_LEFT_CLAMP=4]
[PLAYER_X_RIGHT_CLAMP=148]
inline handle_player_movement {
	a=swcha a&0x10 == {
		a=player_y c+ a-1 a?[PLAYER_Y_TOP_CLAMP] < { 
			player_y=a=[PLAYER_Y_TOP_CLAMP] 
			a=camera_y+1 a?CAMERA_Y_LIMIT < { c- a=camera_y a+0x20 camera_y=a a=camera_y+1 a+0 camera_y+1=a }
		}
		else {
			player_y=a
		}
	}

	a=swcha a&0x20 == {
		a=player_y c- a+1 a?[PLAYER_Y_BOTTOM_CLAMP] >= {
			player_y=a=[PLAYER_Y_BOTTOM_CLAMP]
			a=camera_y a&0xE0 == {
				a=camera_y+1 != {
					c+ a-1 camera_y+1=a
					camera_y=a=0xE0
				}
			}
			else { c+ a-0x20 camera_y=a }
		}
		player_y++
	}
	a=camera_y a>> a>> a>> a>> a>> camera_y_offset=a
	a=8 c+ a-camera_y_offset camera_y_cover=a

	a=swcha a&0x40 == {
		a=player_x c+ a-1 a?[PLAYER_X_LEFT_CLAMP] <{ a=[PLAYER_X_LEFT_CLAMP] } player_x=a
	}
	else {	
		a=swcha a&0x80 == {
			a=player_x c- a+1 a?[PLAYER_X_RIGHT_CLAMP] >= { a=[PLAYER_X_RIGHT_CLAMP] } player_x=a
		}
	}
}


inline set_map_ptr {
	ptr_map_pf0_0+1=a=&>map1_pf0_0
	ptr_map_pf1_0+1=a=&>map1_pf1_0 
	ptr_map_pf2_0+1=a=&>map1_pf2_0
	ptr_map_pf0_1+1=a=&>map1_pf0_1
	ptr_map_pf1_1+1=a=&>map1_pf1_1
	ptr_map_pf2_1+1=a=&>map1_pf2_1
}

main {
	init
	ctpf=a=1
	player_y=player_x=a=50
	cp0=a=0x6A
	set_map_ptr
	{
		sync1
			//handle_camera_movement
			handle_player_movement
		sync2
			// set ptrs to camera movement
			a=camera_y+1
			ptr_map_pf0_0=ptr_map_pf1_0=ptr_map_pf2_0=ptr_map_pf0_1=ptr_map_pf1_1=ptr_map_pf2_1=a
			// sprite positioning
			a=player_x x=0 SetHorizPos wsync hmove=a
			a=player_y c- a+[SPRITE_HEIGHT] player_y_max=a
		sync3
			draw_map_frame
	} always
}
